FUNCTION circulariseNode {
  PARAMETER location.
  // v = sqrt(u((2/r)-(1/a))
  set r_a to ship:apoapsis + ship:body:radius.                //calculates the height from center of planet
  set r_p to ship:periapsis + ship:body:radius.
  set ecc to 1 - (2 / ((r_a / r_p) + 1)).                     //calculates eccentricity
  set sma to (r_p + r_a) / 2.                                 //calculates semi major axis
  set v_a to sqrt(((1 - ecc) * body:mu) / ((1 + ecc) * sma)). //calculations of velcoity
  set v_p to sqrt(((1 + ecc) * body:mu) / ((1 - ecc) * sma)).
  Print "parameters set".
  
  if(location = "apoapsis") {
    set circDV to sqrt(body:mu / r_a) - v_a.                  //calculates dV
    add node(time:seconds + eta:apoapsis, 0, 0, circDV).
  }
  else if(location = "periapsis") {
    set circDV to sqrt(body:mu / r_p) - v_p.                  //calculates dV
    add node(time:seconds + eta:periapsis, 0, 0, circDV).
  }
  else {
    print "Invalid argument!".
    print "Use 'apoapsis' or 'periapsis'!".
  }
}
